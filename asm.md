# Ассемблер [БЭВМ](bcomp.md) #



# Синтаксис #
| **Назначение** | **Синтаксис** | **Пример использования** |
|:-------------------------|:-----------------------|:--------------------------------------------|
| Управление размещением в памяти | `ORG адрес` | `ORG 10` |
| Адресная команда с прямой адресацией | `[метка:] МНЕМОНИКА АРГУМЕНТ` | `MOV R` |
| Адресная команда с косвенной адресацией | `[метка:] МНЕМОНИКА (АРГУМЕНТ)` | `ADD (K)` |
| Безадресная команда | `[метка:] МНЕМОНИКА` | `BEGIN: CLA` |
| Команда ввода-вывода | `[метка:] МНЕМОНИКА АДРЕСВУ` | `OUT 3` |
| Константы | `[метка:] WORD значениe[,значениe...]`<br><code>[[метка:] WORD количество DUP (значение)</code> <table><thead><th> <code>X: WORD ?</code><br><code>Y: WORD X</code><br><code>VALUES: WORD 1,2,3</code><br><code>ARRAY: WORD 10 DUP (?)</code> </th></thead><tbody></tbody></table>

Метки, команды, их аргументы и т.п. должны быть отделены друг от друга пробелом или символом табуляции.<br>
<br>
<h1>Описание директив</h1>
<ol><li><code>ORG address</code> - указывает компилятору, что следующее значение необходимо располагать по указанному адресу. Похожа на пультовую команду "Ввод адреса". Обычно данную директивую достаточно использовать один раз в начале программы.<br>
</li><li><code>WORD</code> - ввод констант и резервирование памяти. Может быть указано одно или более значений. Если в качестве значения указан вопросительный знак, то соответствующая ячейка памяти остаётся неинициализированной. Если указанное значение не удалось распознать как шестнадцатеричное число, то в качестве значения будет использован адрес метки с указанным именем. При использовании в синтаксисе <code>WORD количество DUP (значение)</code> соответствующее значение будет продублировано указанное количество раз.</li></ol>

<h1>Метки</h1>
Метки являются ссылками на соответствующие ячейки памяти. Могут использоваться как аргументы для адресных команд и для инициализации других ячеек адресом, на которую ссылается метка. В имени метки могут использоваться любые символы, однако, в связи с особенностями обработки констант, не рекомендуется использовать имена меток, которые могут быть восприняты как шестнадцатеричное число.<br>
<br>
<h1>Специальные метки</h1>
<ol><li>BEGIN - указывает компилятору на первую выполняемую команду программы. Должна быть указана в любой программе.<br>
</li><li>R - указывает на ячейку, в которой будет располагаться результат. После успешной компиляции, если в программе была обнаружена метка R, <a href='cli.md'>консольная версия БЭВМ</a> выведет адрес соответствующей ячейки памяти.</li></ol>

<h1>Комментарии</h1>
Любой текст в строке после символа ; или # считается комментарием.<br>
<br>
<h1>Ограничения и известные проблемы</h1>
<ol><li>Все вводимые значения воспринимаются в шестнадцатеричной системе и диапозон допустимых значений не проверяется. Следует обратить внимание, что, например, значения <code>8000</code> и <code>-8000</code> будут интерпретированы как одно и тоже число.<br>
</li><li>Аргументы адресных команд воспринимаются как имя метки <b>только</b> если не являются шестнадцатеричным числом.<br>
</li><li>Обработка и диагностика ошибок находятся в зачаточном состоянии.<br>
</li><li>При использовании директивы <code>ORG</code> компилятор не отслеживает возможное перекрытие частей программы. Приведённые ниже код скомпилируется без ошибок, но в памяти БЭВМ окажется некорректная программа.<br>
<pre><code>ORG	10<br>
BEGIN:	CLA<br>
	ADD	X<br>
	ADD	Y<br>
	MOV	R<br>
	HLT<br>
<br>
ORG	12<br>
X:	WORD	DEAD<br>
Y:	WORD	BEEF<br>
R:	WORD	?<br>
</code></pre></li></ol>

<h1>Примеры использования</h1>
<h2>Сложение двух чисел</h2>
<pre><code>ORG	010<br>
BEGIN:	CLA<br>
	ADD	X<br>
	ADD	Y<br>
	MOV	R<br>
	HLT<br>
<br>
ORG	018<br>
R:	WORD	?<br>
X:	WORD	DEAD<br>
Y:	WORD	BEEF<br>
</code></pre>

<h2>Подсчёт количества неотрицательных элементов в массиве</h2>

<h3>Решение 1. Старый стиль</h3>
<pre><code>ORG	00F<br>
	WORD	0020<br>
BEGIN:	WORD	F200,480F,A017,F200,401B,F800,301B,001A,C010,F000,FFFA,0000<br>
ORG	020<br>
	WORD	0001,FFFF,0002,FFFE,0003,FFFD<br>
</code></pre>
<h4>Преимущества:</h4>
<ol><li>Работает.<br>
</li><li>Ввод программы максимально приближен к обычной работе с <a href='bcomp.md'>БЭВМ</a>.<br>
<h4>Недостатки:</h4>
</li><li>Собственно возможности ассемблера не используются.<br>
</li><li>Из текста программы не понятно где расположены команды, а где данные и результат.</li></ol>

<h3>Решение 2. Студенческое</h3>
<pre><code>ORG	00F<br>
	WORD	0020<br>
<br>
BEGIN:	CLA<br>
	ADD	(00F)<br>
	BMI	SKIP<br>
	CLA<br>
	ADD	R<br>
	INC<br>
	MOV	R<br>
SKIP:	ISZ	01A<br>
	BR	BEGIN<br>
	HLT<br>
<br>
	WORD	-6<br>
R:	WORD	?<br>
<br>
ORG	020<br>
X:	WORD	0001, FFFF, 0002, FFFE, 0003, FFFD<br>
</code></pre>
<h4>Преимущества:</h4>
<ol><li>Тоже работает.<br>
<h4>Недостатки:</h4>
</li><li>Из поля зрения выпадают исходные данные хранящие адрес первого элемента массива и их количество.<br>
</li><li>При повторном запуске программы с новыми данными результат может быть некорректным.</li></ol>

<h3>Решение 3. Правильное</h3>
<pre><code>ORG	00F<br>
K:	WORD	?	; Адрес первого элемента массива<br>
<br>
BEGIN:	CLA<br>
	MOV	R<br>
	ADD	(K)<br>
	BMI	SKIP<br>
	CLA<br>
	ADD	R<br>
	INC<br>
	MOV	R<br>
SKIP:	ISZ	N<br>
	BR	BEGIN<br>
	HLT<br>
<br>
N:	WORD	?	; Количество элементов массива<br>
R:	WORD	?	; Результат<br>
<br>
ORG	020<br>
X:	WORD	6 DUP (?)	; Элементы массива<br>
</code></pre>
<h4>Преимущества:</h4>
<ol><li>Видны все исходные данные, используемые программой.<br>
</li><li>При повторном запуске программы с новыми исходными данными результат будет корректным.<br>
<h4>Недостатки:</h4>
</li><li>Перед запуском программы пользователь должен самостоятельно ввести все исходные данные.<br>
</li><li>Пользователь может ошибочно посчитать, что программа может работать только с массивом из 6 элементов.</li></ol>

<h2>Обработка прерываний</h2>
<h3>Требования</h3>
<ol><li>В основной программе должно циклически сдвигаться содержимое ячейки R, всегда содержащее один единичный бит.<br>
</li><li>Раз в десять секунд должно увеличиваться содержимое ячейки T.<br>
</li><li>При запросе от ВУ1 выводить на него значение ячейки X.<br>
</li><li>При запросе от ВУ2 сохранять введённое в РД ВУ2 значение в ячейку X.<br>
</li><li>При запросе от ВУ3 обменивать местами значение его РД и ячейки X.<br>
</li><li>Обмен с ВУ осуществлять в режиме прерывания.<br>
<h3>Решение</h3>
<pre><code>ORG	000<br>
RET:	WORD	?<br>
	BR	INT<br>
<br>
ORG	010<br>
BEGIN:	CLA<br>
	MOV	T<br>
	ADD	CONST<br>
	OUT	0<br>
	EI<br>
	CLA<br>
	INC<br>
LOOP:	ROR<br>
	BCS	LOOP<br>
	MOV	R<br>
	BR	LOOP<br>
<br>
ORG	01C<br>
CONST:	WORD	A<br>
X:	WORD	0<br>
T:	WORD	?<br>
R:	WORD	?<br>
<br>
INT:	MOV	STOR_A<br>
	ROL<br>
	MOV	STOR_C<br>
	TSF	0<br>
	BR	IO1<br>
	CLF	0<br>
	ISZ	T<br>
	NOP<br>
IO1:	TSF	1<br>
	BR	IO2<br>
	CLA<br>
	ADD	X<br>
	OUT	1<br>
	CLF	1<br>
IO2:	TSF	2<br>
	BR	IO3<br>
	CLA<br>
	IN	2<br>
	CLF	2<br>
	MOV	X<br>
IO3:	TSF	3<br>
	BR	END<br>
	CLA<br>
	IN	3<br>
	MOV	TMP<br>
	CLA<br>
	ADD	X<br>
	OUT	3<br>
	CLF	3<br>
	CLA<br>
	ADD	TMP<br>
	MOV	X<br>
END:	CLA<br>
	ADD	STOR_C<br>
	ROR<br>
	CLA<br>
	CMA<br>
	AND	STOR_A<br>
	EI<br>
	BR	(RET)<br>
TMP:	WORD	?<br>
STOR_A:	WORD	?<br>
STOR_C:	WORD	?<br>
</code></pre>
При выполнении данной программы в <a href='gui.md'>графической реализации эмулятора</a> рекомендуется <a href='gui#%D0%A1%D0%BF%D0%B8%D1%81%D0%BE%D0%BA_%D0%B3%D0%BE%D1%80%D1%8F%D1%87%D0%B8%D1%85_%D0%BA%D0%BB%D0%B0%D0%B2%D0%B8%D1%88.md'>уменьшить задержку между тактами</a>.